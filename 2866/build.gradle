import com.android.build.gradle.tasks.PackageApplication

apply plugin: 'com.android.application'

android {
    signingConfigs {
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('libs/ftc.debug.keystore')
            storePassword 'android'
        }
    }
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 19
    }
    compileSdkVersion 24
    buildToolsVersion '24.0.2'
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.3'
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

allprojects {
    repositories {
        jcenter()
        flatDir {
            dirs 'out'
        }
    }
}

dependencies {
    //compile files('libs/android-support-v4.jar')
    compile(name: 'RobotCore-release', ext: 'aar')
    compile(name: 'Hardware-release', ext: 'aar')
    compile(name: 'FtcCommon-release', ext: 'aar')
    compile(name: 'ModernRobotics-release', ext: 'aar')
    compile(name: 'Analytics-release', ext: 'aar')
    compile(name: 'WirelessP2p-release', ext: 'aar')

    compile 'com.android.support:support-compat:24.2.0'
    compile 'com.android.support:support-core-utils:24.2.0'
    compile 'com.android.support:support-core-ui:24.2.0'
    compile 'com.android.support:support-media-compat:24.2.0'
    compile 'com.android.support:support-fragment:24.2.0'
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'org.atteo.classindex:classindex:3.4'
    compile project(':Common-Code')

}

// Package the preprocessed Annotations (ClassIndex) into the APK
tasks.withType(PackageApplication) { pkgTask ->
    android.applicationVariants.all { variant ->
        def String variantName = variant.getName().capitalize()
        if (pkgTask.getName().indexOf(variantName) != -1) {
            copy {
                from new File(buildDir, "intermediates/classes/$variant.dirName/META-INF/annotations/")
                into new File(buildDir, "intermediates/sourceFolderJavaResources/$variant.dirName/META-INF/annotations/");
            }
        }
    }
}
